# Maintainer: Jesse Dubay <jesse@jessedubay.com>
# Contributor: Jason Ryan <jasonwryan@gmail.com>
# Contributor: Steven Allen <steven@stebalien.com>
# Contributor: Army
# Contributor: Bastien Dejean <baskerville@lavabit.com>

_name=sxiv
pkgname="${_name}-git"
pkgver=2015.05.18.g9264a65
pkgrel=1
pkgdesc="Simple (or small or suckless) X Image Viewer"
arch=('i686' 'x86_64')
url="https://github.com/muennich/${_name}"
license=('GPL2')
depends=('imlib2' 'libexif' 'desktop-file-utils')
makedepends=('git')
provides=("${_name}")
conflicts=("${_name}")
install="${_name}.install"
source=("${_name}::git+${url}.git"
        "thumb-zoom-level.patch")
sha256sums=('SKIP'
            '6ead22f016f25e94b804870154eacfe07304d3ac552908ae370cf7d62a347a86')

pkgver() {
  cd "${srcdir}/${_name}"
  git log -1 --format="%cd.g%h" --date=short | sed 's/-/./g'
}

prepare() {
  cd "${srcdir}/${_name}"

  patch -p1 -i "../thumb-zoom-level.patch"

  # use your customisations
  [[ -f $SRCDEST/config.h ]] && cp "$SRCDEST"/config.h .

  # Allow custom FLAGS
  sed -i Makefile -e 's|^CFLAGS\s*=|CFLAGS +=|' -e 's|^LDFLAGS\s*=|LDFLAGS +=|'
}

build() {
  cd "$srcdir/${_name}"
  export CFLAGS="$CFLAGS $CPPFLAGS"
  make PREFIX="/usr"
}

package() {
  cd "$srcdir/${_name}"
  make PREFIX="/usr" DESTDIR="$pkgdir" install
  make -C icon PREFIX="/usr" DESTDIR="$pkgdir" install
  install -Dm644 sxiv.desktop "$pkgdir/usr/share/applications/${_name}.desktop"
}

# vim:set ts=2 sw=2 et:pkgname=sxiv
