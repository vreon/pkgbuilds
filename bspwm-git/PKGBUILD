# Maintainer: Jesse Dubay <jesse@jessedubay.com>
# Contributor: Bastien Dejean <nihilhill@gmail.com>

_pkgname=bspwm
pkgname=${_pkgname}-git
pkgver=0.9.9.gb10ee71
pkgrel=1
pkgdesc='A tiling window manager based on binary space partitioning'
arch=('i686' 'x86_64' 'armv7h')
url="https://github.com/baskerville/${_pkgname}"
license=('custom:BSD')
depends=('libxcb' 'xcb-util' 'xcb-util-wm')
makedepends=('git')
optdepends=('sxhkd-git: to define keyboard and pointer bindings'
            'bar-aint-recursive: for the example panel'
            'xtitle-git: for the example panel'
            'sutils-git: for the example panel'
            'terminus-font: for the example panel')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("git://github.com/baskerville/${_pkgname}.git"
        "desktop-floating-status.patch")
sha256sums=('SKIP'
            'a823902fa225882584b1e8a9ec8598c356d5e9e437e3a048c485546b7f8da9f1')

prepare() {
    cd "$srcdir/$_pkgname"
    patch -p1 -i "../desktop-floating-status.patch"
}

pkgver() {
    cd "$srcdir/$_pkgname"
    git describe --always | sed 's|-|.|g'
}

build() {
    cd "$srcdir/$_pkgname"
    make PREFIX=/usr
}

package() {
    cd "$srcdir/$_pkgname"
    make PREFIX=/usr DESTDIR="$pkgdir" install
    install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
